#!/bin/csh -ef
####
#
#############################################################################
#                                                                           #
# Title: ZORRO Drift-Correction of Movie                                    #
#                                                                           #
# (C) 2dx.org, GNU Plublic License.                                         #
#                                                                           #
# Created..........: 07/21/2016                                             #
# Last Modification: 07/21/2016                                             #
# Author...........: 2dx.org                                                #
#                                                                           #
#############################################################################
#
# SORTORDER: 108
#
# MANUAL: This script runs ZORRO on a movie file, to produce a drift-corrected movie and to produce a drift-corrected and averaged image file.
#
# DISPLAY: imagenumber
# DISPLAY: imagename
# DISPLAY: imagename_original
# DISPLAY: nonmaskimagename
# DISPLAY: imagesidelength
# DISPLAY: comment
# DISPLAY: magnification
# DISPLAY: stepdigitizer
# DISPLAY: Calc_from_sample_pixel
# DISPLAY: sample_pixel
# DISPLAY: Thread_Number
# DISPLAY: movie_stackname_raw
# DISPLAY: movie_stackname
# DISPLAY: KV
# DISPLAY: CS
# DISPLAY: gainfactor
#
#$end_local_vars
#
set bin_2dx = ""
set proc_2dx = ""
set SCRATCH_DISK = ""
#
set tempkeep = ""
set imagename = ""
set nonmaskimagename = ""
set imagenumber = ""
set imagesidelength = ""
set realcell = ""
set magnification = ""
set stepdigitizer = ""
set Calc_from_sample_pixel = ""
set sample_pixel = ""
set realang = ""
set phacon = ""
set RESMIN = ""
set RESMAX = ""
set movie_inmovie = ""
set movie_stackname_raw = ""
set movie_stackname = ""
set Thread_Number = ""
set SCRATCH_DISK = ""
set KV = ""
set CS = ""
set gainfactor = ""
#
#$end_vars
#
set scriptname = 2dx_driftcorrect
\rm -f LOGS/${scriptname}.results
#
set noEMAN = 'y'
#
source ${proc_2dx}/initialize
#
source ${proc_2dx}/2dx_makedirs
#
echo "<<@evaluate>>"
#
echo "<<@progress: 1>>"
#
#################################################################################
${proc_2dx}/linblock "Verifying some parameters"
#################################################################################
#
echo "<<@progress: 5>>"
#
if ( ! -e ${movie_stackname_raw} ) then
  echo "dummy" > zzzzz27765.dm4
  set filename = `ls -1 *.dm4 | sort | head -n 1`
  if ( ${filename} != "zzzzz27765.dm4" ) then
    set movie_stackname_raw = ${filename}
    ${proc_2dx}/linblock "Image ${movie_stackname_raw}.dm4 found."
    echo "set movie_stackname_raw = ${movie_stackname_raw}" >> LOGS/${scriptname}.results
  else
    if ( -e ${movie_stackname} ) then
      if ( ${movie_stackname} !~ "zorro" ) then
        set movie_stackname_raw = ${movie_stackname}
        echo "set movie_stackname_raw = ${movie_stackname_raw}" >> LOGS/${scriptname}.results
      endif
    endif
  endif
endif
#
set movie_stackname_raw_end = `echo ${movie_stackname_raw} | cut -d\. -f2`
if ( ${movie_stackname_raw_end} != 'dm4' && ${movie_stackname_raw_end} != 'mrc' ) then
  ${proc_2dx}/protest "Movie type ${movie_stackname_raw_end} is not 
endif
#
#################################################################################
${proc_2dx}/linblock "Getting movie dimensions"
#################################################################################
#
if ( ${movie_stackname_raw_end} == 'mrc' ) then
  setenv IN ${movie_stackname_raw}
  ${bin_2dx}/2dx_header.exe
  # cat 2dx_header.out
  set NX = `head -n 1 2dx_header.out | tail -n 1 | cut -c43- | awk '{s = $1} END { print s }'`
  set NY = `head -n 1 2dx_header.out | tail -n 1 | cut -c43- | awk '{s = $2} END { print s }'`
  set NZ = `head -n 1 2dx_header.out | tail -n 1 | cut -c43- | awk '{s = $3} END { print s }'`
  echo ":Dimensions of input movie in X,Y,Z are ${NX},${NY},${NZ}"
  \rm 2dx_header.out
else
  set NX = 9999
  set NY = 9999
  set NZ = 9999
endif
#
#############################################################################
#############################################################################
#######  Now Run Zorro ######################################################
#############################################################################
#############################################################################
#
#################################################################################
${proc_2dx}/linblock "Sourcing 2dx_zorro_sub.com"
#################################################################################
#
set n_threads = ${Thread_Number} # number of threads for numexpr and FFTW
set savePNGs = True # Do you want Zorro diagnostic images?
set pixelsize = ${sample_pixel} # pixel size in Angstroems
set a = `echo ${realcell} | sed 's/,/ /g' | awk '{ s = $1 } END { print s }'` # lattice parameter a, Angstroms
set b = `echo ${realcell} | sed 's/,/ /g' | awk '{ s = $2 } END { print s }'` # lattice parameter b, Angstroms
set gamma = ${realang} # crystal rotation parammeter
set outputFolder = "./zorro/"
if ( ! -d zorro ) then
  if ( -e zorro ) then
    \rm -rf zorro
  endif
  \mkdir zorro
endif
set movie_stackname_raw_base = `echo ${movie_stackname_raw} | sed 's/.mrc//g' | sed 's/.dm4//g'`
#
echo "# IMAGE-IMPORTANT: "${movie_stackname_raw} "<Input Movie Stack>" >> LOGS/${scriptname}.results
echo "# IMAGE-IMPORTANT: "${movie_stackname_raw_base}_zorro.mrc "<MRC: Drift-corrected average>" >> LOGS/${scriptname}.results
echo "# IMAGE-IMPORTANT: "${movie_stackname_raw_base}_zorro_filt.mrc "<MRC: Dose-filtered, drift-corrected average>" >> LOGS/${scriptname}.results
echo "# IMAGE-IMPORTANT: "${movie_stackname_raw_base}_zorro_movie.mrc "<MRC: Drift-corrected movie>" >> LOGS/${scriptname}.results
echo "# IMAGE-IMPORTANT: "${movie_stackname_raw_base}.mrc.zor.txt "<TXT: ZORRO logfile>" >> LOGS/${scriptname}.results
#
echo "# IMAGE: "LOGS/2dx_driftcorrect_zorro.txt "<TXT: ZORRO run output>" >> LOGS/${scriptname}.results
#
echo ":: Launching: ${app_anaconda} ${proc_2dx}/movie/2dx_zorro_sub.py ${n_threads} ${savePNGs} ${pixelsize} ${a} ${b} ${gamma} ${outputFolder} ${movie_stackname_raw} ${SCRATCH_DISK} ${KV} ${CS} ${gainfactor} ${stepdigitizer} ${NY} ${NX} ${NZ}"
echo ":: Working on ${movie_stackname_raw}"
echo ":: Output  will be in LOGS/2dx_driftcorrect_zorro.txt"
\rm -f LOGS/2dx_driftcorrect_zorro.txt
${app_anaconda} ${proc_2dx}/movie/2dx_zorro_sub.py ${n_threads} ${savePNGs} ${pixelsize} ${a} ${b} ${gamma} ${outputFolder} ${movie_stackname_raw} ${SCRATCH_DISK} ${KV} ${CS} ${gainfactor} ${stepdigitizer} ${NY} ${NX} ${NZ} >& LOGS/2dx_driftcorrect_zorro.txt
cat LOGS/2dx_driftcorrect_zorro.txt
#
#################################################################################
${proc_2dx}/linblock "Back in 2dx_driftcorrect.com"
#################################################################################
#
if ( -e ${movie_stackname_raw_base}_zorro_movie.mrcs ) then
  #################################################################################
  ${proc_2dx}/linblock "Switching project to zorro-corrected movie and average"
  #################################################################################
  \mv -f ${movie_stackname_raw_base}_zorro_movie.mrcs ${movie_stackname_raw_base}_zorro_movie.mrc
  echo "set movie_stackname = ${movie_stackname_raw_base}_zorro_movie" >> LOGS/${scriptname}.results
  echo "set nonmaskimagename = ${movie_stackname_raw_base}_zorro_filt" >> LOGS/${scriptname}.results
  echo "set imagename = ${movie_stackname_raw_base}_zorro_filt" >> LOGS/${scriptname}.results
  if ( ${nonmaskimagename} != ${movie_stackname_raw_base}_zorro_filt ) then
    if ( -e ${nonmaskimagename}_automask.mrc ) then
      \cp -f ${nonmaskimagename}_automask.mrc ${movie_stackname_raw_base}_zorro_filt_automask.mrc
    endif 
    if ( -e ${nonmaskimagename}_automask_mask.mrc ) then
      \cp -f ${nonmaskimagename}_automask_mask.mrc ${movie_stackname_raw_base}_zorro_filt_automask_mask.mrc
    endif 
  endif
  \rm -f m${movie_stackname_raw_base}_zorro_filt.mrc
  #
  \mv -f fig/* zorro
  \rm -rf fig
  echo "# IMAGE: "./zorro/${movie_stackname_raw}_FRC.png "<PNG Zorro: FRC among adjacent frames>" >> LOGS/${scriptname}.results
  echo "# IMAGE: "./zorro/${movie_stackname_raw}-logisticWeights.png "<PNG: Logistic weights plot>" >> LOGS/${scriptname}.results
  echo "# IMAGE: "./zorro/${movie_stackname_raw}_peaksigTriMat.png "<PNG: Peak significance matrix>" >> LOGS/${scriptname}.results
  echo "# IMAGE: "./zorro/${movie_stackname_raw}_translations.png "<PNG: Drift profile plot>" >> LOGS/${scriptname}.results
  echo "# IMAGE: "./zorro/${movie_stackname_raw}_pixRegError.png "<PNG: Pixel registration error X,Y>" >> LOGS/${scriptname}.results
  echo "# IMAGE: "./zorro/${movie_stackname_raw}_imageSum "<PNG: Drift-cor. average>" >> LOGS/${scriptname}.results
  echo "# IMAGE: "./zorro/${movie_stackname_raw}_filtSum.png "<PNG: Drift-cor., filtered average >" >> LOGS/${scriptname}.results
  echo "# IMAGE: "./zorro/${movie_stackname_raw}_FFTSum.png "<PNG: Drift-cor. average (FFT)>" >> LOGS/${scriptname}.results
  echo "# IMAGE: "./zorro/${movie_stackname_raw}_polarFFTSum.png "<PNG: Drift-cor. average (Polar FFT)>" >> LOGS/${scriptname}.results
endif
#
echo " "
echo ": "
echo ":: "
echo "::Now run the script CLEANUP, then start with INIT FILES AND PARAMETERS etc."
echo ":: "
echo ": "
echo " "
#
echo "<<@evaluate>>"
#
##########################################################################
${proc_2dx}/linblock "${scriptname} - normal end."
##########################################################################
##
#
echo "<<@progress: 100>>"
exit
#
# These are listed here to make sure they appear in the 2dx_image GUI:
#
python ${proc_2dx}/movie/2dx_zorro_sub.py
