#!/bin/csh -ef
####
#
#############################################################################
#                                                                           #
# Title: Refine Defocus from Merged Data                                    #
#                                                                           #
# (C) 2dx.org, GNU Plublic License.                                         #
#                                                                           #
# Created..........: 12/03/2007                                             #
# Last Modification: 12/03/2007                                             #
# Author...........: 2dx.org                                                #
#                                                                           #
#############################################################################
#
# SORTORDER: 77
#
# MANUAL: This script calls the MRC program ctfsearch to refine the defocus based on comparisson with the merged dataset.
#
# MANUAL: Be aware that this program may make things worse, if the data are noisy. Use with care, and first try to run it with the "Overwrite data" button set to "no". If the output appears to make sense, you can re-run it with "Overwrite data" set to "yes". 
#
#=============================================================================
# SECTION: Algorithm Selection
#=============================================================================
#
# LABEL: Overwrite data
# LEGEND: Define if the results should overwrite the database or not.
# EXAMPLE: overwrite = "n"
# HELP: http://2dx.org/documentation/2dx-software/parameters/searchdefocus
# TYPE: Bool "y;n"
set overwrite = "n"
#
#=============================================================================
#
# DISPLAY: RESMIN
# DISPLAY: RESMAX
# DISPLAY: lattice
# DISPLAY: realang
# DISPLAY: ALAT
# DISPLAY: TLTAXIS
# DISPLAY: TLTANG
# DISPLAY: TLTAXA
# DISPLAY: TAXA
# DISPLAY: TANGL
# DISPLAY: defocus
# DISPLAY: defocusbackup
# DISPLAY: SYM
# DISPLAY: phaori
# DISPLAY: revhk
# DISPLAY: revhnd
# DISPLAY: sgnxch
# DISPLAY: rot180
# DISPLAY: rot90
# DISPLAY: IQMAX
# DISPLAY: comment
# DISPLAY: ctf_ttf
# DISPLAY: ctfsearch_df_step
# DISPLAY: ctfsearch_df_range
# DISPLAY: ctfsearch_ncyc
# DISPLAY: ctfsearch_iphase
# DISPLAY: phacon
# DISPLAY: SYN_Unbending
#
#$end_local_vars
#
set bin_2dx = ""
set proc_2dx = ""
set SCRATCH_DISK = ""
set FFTIR_DISK = ""
set imagename = ""
set nonmaskimagename = ""
set imagesidelength = ""
set imagenumber = ""
set realcell = ""
set imagesidelength = ""
set magnification = ""
set stepdigitizer = ""
set tempkeep = ""
set revhk = ""
set rot180 = ""
set rot90 = ""
set revhnd = ""
set sgnxch = ""
set realang = ""
set RESMIN = ""
set RESMAX = ""
set ALAT = "" 
set lattice = ""
set IQMAX = ""
set defocus = ""
set defocusbackup = ""
set TLTAXIS = ""
set TLTANG = ""
set TLTAXA = ""
set TAXA = ""
set TANGL = ""
set SYM = ""
set phaori = ""
set CS = ""
set KV = ""
set RMAG = ""
set ctf_ttf = ""
set ctfsearch_df_step = ""
set ctfsearch_df_range = ""
set ctfsearch_ncyc = ""
set ctfsearch_iphase = ""
set phacon = ""
set SYN_Unbending = ""
#
#$end_vars
#
set scriptname = 2dx_refinedefocus
#
\rm -f LOGS/${scriptname}.results
#
set ccp4_setup = 'y'
source ${proc_2dx}/initialize
#
#############################################################################
${proc_2dx}/linblock "${scriptname}" 
#############################################################################  
#
echo bin_2dx = ${bin_2dx}
echo proc_2dx = ${proc_2dx}
#
echo SYM = ${SYM}
source ${proc_2dx}/2dx_sym2spcgrp_sub.com
echo spcgrp = ${spcgrp}
echo " "
#
#
source ${proc_2dx}/2dx_makedirs
#
echo "<<@progress: 1>>"
#
if ( ${rot180} == 'y' ) then
  set rot180val = '1'
else
  set rot180val = '0'
endif
#
if ( ${rot90} == 'y' ) then
  set rot90val = '1'
else
  set rot90val = '0'
endif
#
if ( ${revhk} == 'y' ) then
  set revhkval = '1'
else
  set revhkval = '0'
endif
#
if ( ${revhnd} == 'y' ) then
  set revhndval = '1'
else
  set revhndval = '0'
endif
#
echo sgnxch = ${sgnxch}
#
if ( ${sgnxch} == "y" ) then
  set sgnxchval = 1
  set phaorix = `echo ${phaori_local} | cut -d\, -f1 `
  set phaoriy = `echo ${phaori_local} | cut -d\, -f2 | awk '{ s = -$1 } END { print s }'`
  set phaori_local = `echo ${phaorix},${phaoriy}`
else
  set sgnxchval = 0
endif
#
\rm -f LOGS/${imagename}.ctfs.wilson.log
\rm -f ctfsearch.dat
\rm -f WILSONCTF
\rm -f SUMMARY
\rm -f TMP345875.tmp
\rm -f TMP345876.tmp
#
# Test if reference is existing:
# set mergedat = "../merge/latfittedref_nophaerr.mtz"
set mergedat = "../merge/merge3Dref.mtz"
set averagedat = "../merge/APH/avrg.hk"
set iref = 2
if ( ! -e ${mergedat} ) then
  set mergedat = "../../merge/merge3Dref.mtz"
  set averagedat = "../../merge/APH/avrg.hk"
  if ( ! -e ${mergedat} ) then
    set mergedat = "../merge/merge.mtz"
    set averagedat = "../merge/APH/avrg.hk"
    set iref = 0
    if ( ! -e ${mergedat} ) then
      set mergedat = "../../merge/merge.mtz"
      set averagedat = "../../merge/APH/avrg.hk"
      set iref = 0
      if ( ! -e ${mergedat} ) then
        set iref = "-1"
        ${proc_2dx}/linblock "WARNING. No merged reference dataset found."
        ${proc_2dx}/linblock "Neither ../merge/merge3D.mtz nor ../../merge/merge3D.mtz exist."
        ${proc_2dx}/linblock "and neither ../merge/merge.mtz nor ../../merge/merge.mtz exist."
        ${proc_2dx}/linblock "Using all intensities = 100 as reference."
      endif
    endif
  endif
endif
#
${proc_2dx}/linblock "Using reference datafile ${mergedat}"
${proc_2dx}/linblock "Using ctfsearch with iref=${iref}"
#
echo "<<@progress: 10>>"
#
if ( ${ctf_ttf} != 'CTF' ) then
  ${proc_2dx}/protest "ERROR: You cannot run ctfsearch with TTF-corrected data."
endif
#
if ( ${SYN_Unbending} == "0" ) then
  # FouFilter Unbending result is used:
  set infile_nolimit = ${imagename}.fou.nolimit.aph
  #
  # A check in case somebody just upgraded from an earlier version of 2dx:
  cd APH
  if ( ! -e ${infile_nolimit} ) then
    if ( -e ${imagename}.nolimit.aph ) then
      \cp ${imagename}.nolimit.aph ${infile_nolimit}
    endif
  endif
  set infile_limit = ${imagename}.fou.limit.aph
  if ( ! -e ${infile_limit} ) then
    if ( -e ${imagename}.limit.aph ) then
      \cp ${imagename}.limit.aph ${infile_limit}  
    endif
  endif
  cd ..
  #
else
  # Synthetical Unbending result is used:
  set infile_nolimit = ${imagename}.syn.nolimit.aph
endif
#
echo "# IMAGE-IMPORTANT: ${imagename}.mrc <Image>" >> LOGS/${scriptname}.results
echo "# IMAGE-IMPORTANT: "${nonmaskimagename}.mrc "<Raw Image>" >> LOGS/${scriptname}.results
echo "# IMAGE-IMPORTANT: FFTIR/${imagename}.red.mrc <Downsampled Image>" >> LOGS/${scriptname}.results
echo "# IMAGE-IMPORTANT: FFTIR/${imagename}.fft.mrc <FFT of Image>" >> LOGS/${scriptname}.results
echo "# IMAGE-IMPORTANT: FFTIR/${imagename}.red.fft.mrc <FFT of Downsampled Image>" >> LOGS/${scriptname}.results
#
if ( ! -e APH/${infile_nolimit} ) then
  ${proc_2dx}/protest "ERROR: APH/${infile_nolimit} does not exist."
endif
#
setenv INIMAGE APH/${infile_nolimit}
echo "# IMAGE-IMPORTANT: APH/${infile_nolimit} <APH: Input data file>" >> LOGS/${scriptname}.results
#
set IAQP2 = 0
set fittitle = "ctfsearch2 run"
#
######################################################################################
if ( $iref == 2 ) then
  ######################################################################################
  #
  setenv HKLIN ${mergedat}
  echo "# IMAGE-IMPORTANT: ${mergedat} <MTZ: Input reference file>" >> LOGS/${scriptname}.results
  if ( ! -e ${mergedat} ) then
    ${proc_2dx}/protest "ERROR: Reference file not found: ${mergedat}"
  endif
  #
  # set spcgrp = 1
  set spcgrp_local = 1
  #
  #
  echo ": now comes ctfsearch2 with:"
  echo ":${bin_2dx}/ctfsearch2.exe"
  echo ": F F ${IQMAX}                                         ! LIST LISTW QMAX"
  echo ":${lattice} ${imagesidelength} ${stepdigitizer} ${magnification}       ! AX AY BX BY ISIZEX DSTEP XMAG"
  echo ":${defocus} ${CS} ${KV} ${phacon}                           ! DFMID1 DFMID2 ANGAST CS KV PHACON"
  echo ":${fittitle}                                     ! TITLE FOR PLOT OF FINAL BEST FIT."
  echo ":${phaori} ${TAXA} ${TANGL} ${revhkval} ${rot180val} ${sgnxchval} ${rot90val} ${IAQP2}        ! ORIGH ORIGK TAXA TANGL REVHK ROT180 SGNXCH ROT90 IAQP2"
  echo ":${RESMIN} ${RESMAX}                             ! RESMIN RESMAX"
  echo ":${spcgrp_local} ${iref} ${ctfsearch_ncyc} ${ctfsearch_iphase} ${ctfsearch_df_step} ${ctfsearch_df_range} ! ISPGRP,IREF,NCYC,IPHASE,ctfsearch_df_step,ctfsearch_df_range"
  echo ":LABIN AMP=F PHASE=PHI FOM=FOM SIG=SIGA          ! IF IREF=2 MTZ CONTROL"
  echo " "
  #
  ${bin_2dx}/ctfsearch2.exe << eot
F F ${IQMAX}                                         ! LIST LISTW QMAX
${lattice} ${imagesidelength} ${stepdigitizer} ${magnification}       ! AX AY BX BY ISIZEX DSTEP XMAG
${defocus} ${CS} ${KV} ${phacon}                         ! DFMID1 DFMID2 ANGAST CS KV PHACON
${fittitle}                                     ! TITLE FOR PLOT OF FINAL BEST FIT.
${phaori} ${TAXA} ${TANGL} ${revhkval} ${rot180val} ${sgnxchval} ${rot90val} ${IAQP2}       ! ORIGH ORIGK TAXA TANGL REVHK ROT180 SGNXCH ROT90 IAQP2
${RESMIN} ${RESMAX}                             ! RESMIN RESMAX
${spcgrp_local} ${iref} ${ctfsearch_ncyc} ${ctfsearch_iphase} ${ctfsearch_df_step} ${ctfsearch_df_range} ! ISPGRP,IREF,NCYC,IPHASE,ctfsearch_df_step,ctfsearch_df_range
LABIN AMP=F PHASE=PHI FOM=FOM SIG=SIGA          ! IF IREF=2 MTZ CONTROL
eot
  #
  ######################################################################################
else if ( $iref == 0 ) then
  ######################################################################################
  #
  setenv INIMAGE APH/${imagename}.nolimit.aph
  if ( ! -e ${averagedat} ) then
    ${proc_2dx}/protest "ERROR: ${averagedat} not found."
  endif
  setenv INREF ${averagedat}
  #
  ${bin_2dx}/ctfsearch2.exe << eot
F F ${IQMAX}                                                 ! LIST LISTW QMAX
${lattice} ${imagesidelength} ${stepdigitizer} ${magnification}               ! AX AY BX BY ISIZEX DSTEP XMAG
${defocus} ${CS} ${KV} ${phacon}                                 ! DFMID1 DFMID2 ANGAST CS KV PHACON
${fittitle}                                             ! TITLE FOR PLOT OF FINAL BEST FIT.
${phaori} ${TAXA} ${TANGL} ${revhkval} ${rot180val} ${sgnxchval} ${rot90val} ${IAQP2}       ! ORIGH ORIGK TAXA TANGL REVHK ROT180 SGNXCH ROT90 IAQP2
${RESMIN} ${RESMAX}                                     ! RESMIN RESMAX
${spcgrp} ${iref} ${ctfsearch_ncyc} ${ctfsearch_iphase} ${ctfsearch_df_step} ${ctfsearch_df_range}        ! ISPGRP,IREF,NCYC,IPHASE,ctfsearch_df_step,ctfsearch_df_range
                                                        ! FC SIGFC PHCAL FOM or LABIN AMP SIG PHS FOM
eot
  #
  ######################################################################################
else if ( $iref == '-1' ) then
  ######################################################################################
  #
  setenv INIMAGE APH/${imagename}.nolimit.aph
  if ( ! -e ${averagedat} ) then
    ${proc_2dx}/protest "ERROR: ${averagedat} not found."
  endif
  setenv INREF ${averagedat}
  #
  ${bin_2dx}/ctfsearch2.exe << eot
F F ${IQMAX}                                         ! LIST LISTW QMAX
${lattice} ${imagesidelength} ${stepdigitizer} ${magnification}       ! AX AY BX BY ISIZEX DSTEP XMAG
${defocus} ${CS} ${KV} ${phacon}                           ! DFMID1 DFMID2 ANGAST CS KV PHACON
${fittitle}                                     ! TITLE FOR PLOT OF FINAL BEST FIT.
${phaori} ${TAXA} ${TANGL} ${revhkval} ${rot180val} ${sgnxchval} ${rot90val} ${IAQP2}        ! ORIGH ORIGK TAXA TANGL REVHK ROT180 SGNXCH ROT90 IAQP2
${RESMIN} ${RESMAX}                             ! RESMIN RESMAX
${spcgrp} ${iref} ${ctfsearch_ncyc} ${ctfsearch_iphase} ${ctfsearch_df_step} ${ctfsearch_df_range} ! ISPGRP,IREF,NCYC,IPHASE,ctfsearch_df_step,ctfsearch_df_range
                                                ! FC SIGFC PHCAL FOM or LABIN AMP SIG PHS FOM
eot
  #
endif
######################################################################################
######################################################################################
#
if ( -e WILSONCTF ) then
  \mv -f WILSONCTF LOGS/${imagename}.ctfs.wilson.log
endif
if ( -e SUMMARY ) then
  \mv -f SUMMARY LOGS/${imagename}.ctfs.summary.log
endif
#
set olddef = ${defocus}
set tmpdef = "`cat TMP345876.tmp`"
set defocus = `echo ${tmpdef} | sed 's/ /,/g'`
#
\rm -f TMP345875.tmp
\rm -f TMP345876.tmp
#
${proc_2dx}/linblock "Old defocus is ${olddef}"
${proc_2dx}/linblock "New defocus is ${defocus}"
#
if ( ${overwrite} == "y" ) then
  echo "set defocus = ${defocus}" >> LOGS/${scriptname}.results
  ${proc_2dx}/linblock "Using new defocus of ${defocus}"
endif
#
##########################################################################
${proc_2dx}/linblock "${scriptname} - normal end."
##########################################################################
#
echo "<<@progress: 100>>"
#
