#!/bin/csh -ef
####
#
#############################################################################
#                                                                           #
# Title: Generate Map                                                       #
#                                                                           #
# (C) 2dx.org, GNU Plublic License.                                         #
#                                                                           #
# Created..........: 02/20/2006                                             #
# Last Modification: 02/20/2006                                             #
# Author...........: 2dx.org                                                #
#                                                                           #
#############################################################################
#
# SORTORDER: 90
#
# REMARK: The following section defines the text in the Manual pane of the GUI:
# MANUAL: This script makes use of CCP4 commands to generate the final projection map.
# MANUAL: The final output is an MRC-format image file that shows 2x2 unit cells. 
# MANUAL: The PostScript output file is skewed according to any potential non-square non-rectangular lattice, while the MRC-format file has the skewing information stored in the header.
#
# REMARK: The following section defines variables that will be displayed in the GUI:
#
# LABEL: Show APH files?
# LEGEND: This switch decides, if this should be listed in the Images pane.
# EXAMPLE: show_hkl = "y"
# HELP: http://2dx.org/documentation/2dx-software/parameters
# TYPE: Bool "y;n"
set show_hkl = "n"
#
# LABEL: Show Phase Residual TXT files?
# LEGEND: This switch decides, if this should be listed in the Images pane.
# EXAMPLE: show_phares = "y"
# HELP: http://2dx.org/documentation/2dx-software/parameters
# TYPE: Bool "y;n"
set show_phares = "n"
#
# LABEL: Show Point Spread Function (PSF) files?
# LEGEND: This switch decides, if this should be listed in the Images pane.
# EXAMPLE: show_PSF = "y"
# HELP: http://2dx.org/documentation/2dx-software/parameters
# TYPE: Bool "y;n"
set show_PSF = "n"
#
# DISPLAY: SYN_Unbending
# DISPLAY: ML_doit
# DiSPLAY: ML_use_for_merging
# DISPLAY: RESMAX
# DISPLAY: RESMIN
# DISPLAY: ALAT
# DISPLAY: tempfac
# DISPLAY: imagename_original
# DISPLAY: SYM
# DISPLAY: phaori
# DISPLAY: phaoriFouFilter
# DISPLAY: zstarrange
# DISPLAY: zstarrange_real
# DISPLAY: Calc_from_zstarrange
# DISPLAY: ctfrev
# DISPLAY: realang
# DISPLAY: realcell
# DISPLAY: rot90
# DISPLAY: rot180
# DISPLAY: revhk
# DISPLAY: revhnd
# DISPLAY: sgnxch
# DISPLAY: revxsgn
# DISPLAY: tempkeep
# DISPLAY: comment
# DISPLAY: produceSpiderMap
# DISPLAY: mapscale
# DISPLAY: unitcellnumber
# DISPLAY: npo_cntrs_step
# DISPLAY: npo_cntrs_below 
# DISPLAY: MergeIQMAX
# DISPLAY: movie_enable
#
# REMARK: The following section defines the variables that the GUI will fill in from the database, before launching this script:
#$end_local_vars
#
set bin_2dx = ""
set proc_2dx = ""
set SCRATCH_DISK = ""
#
set SYN_Unbending = ""
set PHASEORI_done = ""
#
set produceSpiderMap = ""
set mapscale = ""
set unitcellnumber = ""
set SYN_Unbending = ""
set imagename = ""
set nonmaskimagename = ""
set imagenumber = ""
set imagesidelength = ""
set tempkeep = ""
set RESMIN = ""
set RESMAX = ""
set phacon = ""
set CS = ""
set KV = ""
set ALAT = ""
set lattice = ""
set realang = ""
set realcell = ""
set magnification = ""
set stepdigitizer = ""
set TLTANG = ""
set TLTAXIS = ""
set TLTAXA = ""
set phaori = ""
set phaoriFouFilter = ""
set rot180 = ""
set revhk = ""
set rot90 = ""
set revhnd = ""
set sgnxch = ""
set revxsgn = ""
set ctfrev = ""
set beamtilt = ""
set zstarrange = ""
set Calc_from_zstarrange = ""
set zstarrange_real = ""
set tempfac = ""
set TAXA = ""
set TANGL = ""
set ML_doit = ""
set SYM = ""
set npo_cntrs_step = ""
set npo_cntrs_below = ""
set MergeIQMAX = ""
set movie_enable = ""
set ctfcor_imode = ""
set RFACAMP = ""
#
#$end_vars
#
echo "<<@progress: 1>>"
#
set ccp4_setup = 'y'
source ${proc_2dx}/initialize
#
set scriptname = 2dx_generateMAP
#
source ${proc_2dx}/2dx_makedirs 
#
\rm -f LOGS/${scriptname}.results
#
echo "<<@evaluate>>"
#
set date = `date`
echo date = ${date}
#
set iname = image_ctfcor
#
if ( ${Calc_from_zstarrange} == "y" ) then
  set zstarrange_real = `echo ${zstarrange} ${ALAT} | awk '{ s = 1.0 / ( $1 ) } END { print s }'`
  echo "set zstarrange_real = ${zstarrange_real}" >> LOGS/${scriptname}.results
  ${proc_2dx}/linblock "Calculating vertical resolution as ${zstarrange_real} Angstroems."
else
  set zstarrange = `echo ${zstarrange_real} ${ALAT} | awk '{ s = 1.0 / ( $1 ) } END { print s }'`
  echo "set zstarrange = ${zstarrange}" >> LOGS/${scriptname}.results   
  ${proc_2dx}/linblock "Calculating zstarrange as ${zstarrange} (with 0.5 = Nyquist resolution)."
endif
#
set ABANG = `echo $realang | awk '{s=180-$1} END {print s}'`
echo ABANG = ${ABANG}
set IAQP2 = 0
set IVERBOSE = 1
set LOGOUTPUT = F
set LPROTFOUFIL = T
set phastepnum = 1
set phastep = 0.1
#
#############################################################################
#
set SYM_sub = 'p1'
set spcgrp = "1"
set spcgroup_num = "1"
set CCP4_SYM = "1"
set ML_sym_val = "1"
#
set symmetry = 'p1'
set spcgrp_local = 1
set produceSpiderMapLocal = ${produceSpiderMap}
set CCP4_SYM = 1
set rotate_to_Z = "no"
#
# origtilt does something strange when working with spcgrp>1. (TODO).
# Symmetrization is therefore here only done by the CCP4 programs.
#
set spcgrp_first = 1
echo spcgrp_first = ${spcgrp_first}
#
echo spcgrp = ${spcgrp}
echo SYM_sub = ${SYM_sub}
echo CCP4_SYM = ${CCP4_SYM}
echo rotate_to_Z = ${rotate_to_Z}
#
#
#############################################################################
echo ":: "
${proc_2dx}/linblock "Sourcing generateMap script for Unbending reconstruction in p1 symmetry"
echo ":: "
#############################################################################
#############################################################################
#############################################################################
#
set aphfile = APH/${iname}_fou_unbent_ctf.aph
#
# A check in case somebody just upgraded from an earlier version of 2dx:
if ( ! -e ${aphfile} ) then
  if ( -e APH/${imagename}_ctf.aph ) then
    \cp APH/${imagename}_ctf.aph ${aphfile}
  endif
endif
#
if ( ${show_hkl}x == "yx" ) then
  echo "# IMAGE: ${aphfile} <APH: Input Unbending Amp&Phase File [H,K,A,P(CTF Phase flipped),IQ,Back,CTF]>" >> LOGS/${scriptname}.results
endif
echo "# IMAGE: PS/${iname}-quadserch2b.ps <PS: Vector Plot of distortions 2>" >> LOGS/${scriptname}.results
echo "# IMAGE: PS/${iname}_ctf.ps <PS: IQ Plot after CTF correction>" >> LOGS/${scriptname}.results
#
# echo "# IMAGE: frame_quadserch.ps <PS: QUADSERCH Movie Profiles>" >> LOGS/${scriptname}.results
# echo "# IMAGE: frame_unbending.ps <PS: CCUNBEND Movie Profiles>" >> LOGS/${scriptname}.results
set prefix = ""
set prename = "Unb.II "
#
set phaorix = `echo ${phaori} | cut -d\, -f1 | awk '{ s =  $1 } END { print s }'`
set phaoriy = `echo ${phaori} | cut -d\, -f2 | awk '{ s =  $1 } END { print s }'`
set phaori_local = `echo ${phaorix},${phaoriy}`
#############################################################################
# Unbend II
source ${proc_2dx}/2dx_generateMap_sub.com
#############################################################################
#
echo "<<@progress: 10>>"
#
#
# Synthetic Unbending result is used:
set aphfile = APH/${iname}_U2-Syn_ctf.aph
if ( -e ${aphfile} ) then
    echo ":: "
    ${proc_2dx}/linblock "Generating map from U2-Syn unbending"
    echo ":: "
    if ( ${show_hkl}x == "yx" ) then
      echo "# IMAGE: ${aphfile} <APH: Amp&Phase from U2-Syn unbending>" >> LOGS/${scriptname}.results
    endif
    set prefix = "U2-Syn_"
    set prename = "U2-Syn "
    #
    set phaori_local = ${phaori}
    #############################################################################
    # U2-Syn
    source ${proc_2dx}/2dx_generateMap_sub.com
    #############################################################################
else
    echo ":: "
    ${proc_2dx}/linblock "Not generating map from U2-Syn processing, file not found: ${aphfile}"
endif
#
#
echo "<<@progress: 20>>"
#
#
set aphfile = APH/ML_result.aph
if ( -e ${aphfile} ) then
  echo ":: "
  ${proc_2dx}/linblock "Sourcing generateMap script for ML reconstruction in p1 symmetry"
  echo ":: "
  if ( ${show_hkl}x == "yx" ) then
    echo "# IMAGE: ${aphfile} <APH: ML Input Amp&Phase File>" >> LOGS/${scriptname}.results
  endif
  set prefix = "ML_"
  set prename = "ML "
  #
  set phaori_local = "0.0,0.0"
  #############################################################################
  # ML
  source ${proc_2dx}/2dx_generateMap_sub.com
  #############################################################################
  #
endif
#
echo "<<@progress: 30>>"
#
set aphfile = APH/${iname}_movie_fou_ctf.aph
if ( -e ${aphfile} ) then
    echo ":: "
    ${proc_2dx}/linblock "Generating map from MovieA processing"
    echo ":: "
    if ( ${show_hkl}x == "yx" ) then
      echo "# IMAGE: ${aphfile} <APH: Amp&Phase from MovieA processing>" >> LOGS/${scriptname}.results
    endif
    set prefix = "movie_"
    set prename = "MovieA "
    #
    set phaori_local = ${phaori}
    #############################################################################
    # Movie A
    source ${proc_2dx}/2dx_generateMap_sub.com
    #############################################################################
else
    echo ":: "
    ${proc_2dx}/linblock "Not generating map from MovieA processing, file not found: ${aphfile}"
endif
#
echo "<<@progress: 40>>"
#
set aphfile = APH/${iname}_movie_syn_ctf.aph
if ( -e ${aphfile} ) then
      echo ":: "
      ${proc_2dx}/linblock "Generating map from MovieA U2-Syn processing"
      echo ":: "
      if ( ${show_hkl}x == "yx" ) then
        echo "# IMAGE: ${aphfile} <APH: Amp&Phase from MovieA U2-Syn processing>" >> LOGS/${scriptname}.results
      endif
      set prefix = "movie_syn_"
      set prename = "MA_Syn "
      #
      set phaori_local = ${phaori}
      #############################################################################
      # Movie A U2-Syn
      source ${proc_2dx}/2dx_generateMap_sub.com
      #############################################################################
else
      echo ":: "
      ${proc_2dx}/linblock "Not generating map from MovieA U2-Syn processing, file not found: ${aphfile}"
endif
#
echo "<<@progress: 50>>"
#
set aphfile = APH/${iname}_movieB_fou_ctf.aph
if ( -e ${aphfile} ) then
    echo ":: "
    ${proc_2dx}/linblock "Generating map from MovieB processing"
    echo ":: "
    if ( ${show_hkl}x == "yx" ) then
      echo "# IMAGE: ${aphfile} <APH: Amp&Phase from MovieB processing>" >> LOGS/${scriptname}.results
    endif 
    set prefix = "movieB_"
    set prename = "MovieB "
    #
    set phaori_local = ${phaori}
    #############################################################################
    # Movie B
    source ${proc_2dx}/2dx_generateMap_sub.com
    #############################################################################
else
    echo ":: "
    ${proc_2dx}/linblock "Not generating map from MovieB processing, file not found: ${aphfile}"
endif
#
echo "<<@progress: 60>>"
#
set aphfile = APH/${iname}_movieB_syn_ctf.aph
if ( -e ${aphfile} ) then
      echo ":: "
      ${proc_2dx}/linblock "Generating map from MovieB U2-Syn processing"
      echo ":: "
      if ( ${show_hkl}x == "yx" ) then
        echo "# IMAGE: ${aphfile} <APH: Amp&Phase from MovieB U2-Syn processing>" >> LOGS/${scriptname}.results
      endif
      set prefix = "movieB_syn_"
      set prename = "MB_Syn "
      #
      set phaori_local = ${phaori}
      #############################################################################
      # Movie B U2-Syn
      source ${proc_2dx}/2dx_generateMap_sub.com
      #############################################################################
else
      echo ":: "
      ${proc_2dx}/linblock "Not generating map from MovieB U2-Syn processing, file not found: ${aphfile}"
endif
#
echo ":: "
#
if ( ! -e ${nonmaskimagename}_ref.mrc ) then
  if ( -e ${imagename}_ref.mrc ) then
    \mv -f ${imagename}_ref.mrc ${nonmaskimagename}_ref.mrc 
  endif
endif
echo "# IMAGE-IMPORTANT: ${nonmaskimagename}_ref.mrc <Projected Reference>"  >> LOGS/${scriptname}.results
#
#############################################################################
#############################################################################
#############################################################################
#
echo ":: Done."
echo "<<@progress: 100>>"
#
##########################################################################
${proc_2dx}/linblock "${scriptname} - normal end."
##########################################################################
#
exit
##
# Just to make sure it is displayed in the 2dx GUI:
source ${proc_2dx}/2dx_checklattice_sub.com 
#
#

