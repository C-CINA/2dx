#!/bin/csh -ef
#############################################################################
#                                                                           #
# Title: Import Images & Movies                                             #
#                                                                           #
# (C) 2dx.org, GNU Plublic License.                                         #
#                                                                           #
# Created..........: 10/28/2014                                             #
# Last Modification: 10/28/2014                                             #
# Author...........: 2dx.org                                                #
#                                                                           #
#############################################################################
#
# SORTORDER: 7 
#
# MANUAL: This script allows to import dose-fractionated movies and their drift-corrected average images.
#
# DISPLAY: movie_import_dir
# DISPLAY: movie_import_imagenumber
# DISPLAY: movie_import_target_dir
# DISPLAY: movie_import_type
# DISPLAY: movie_import_aligned_stack
# DISPLAY: movie_import_DC_averages
# DISPLAY: movie_import_DC_stacks
#
#$end_local_vars
#
# This defines some variables that we might need:
set proc_2dx = ""
set bin_2dx = ""
set app_2dx_image = ""
set SCRATCH_DISK = ""
#
set movie_import_dir = ""
set movie_import_imagenumber = ""
set movie_import_target_dir = ""
set movie_import_type = ""
set movie_import_aligned_stack = ""
set movie_import_DC_averages = ""
set movie_import_DC_stacks = ""
#
#$end_vars
#
# This sets the scriptname variable:
set scriptname = 2dx_moviemode_import
#
# This removes the old logfile, so that a new fresh one can be created:
\rm -f LOGS/${scriptname}.results
#
# This initializes the ccp4 environment, and other variables.
set ccp4_setup = 'y'
source ${proc_2dx}/initialize
#
# This advances the progress bar to the 1% position:
echo "<<@progress: 1>>"
#
# This creates all needed subdirectories in the current merge directory, in case they are missing:
source ${proc_2dx}/2dx_makedirs 
#
# This memorizes the current merge directory under the variable "olddir":
set olddir = $PWD
#
if ( ${movie_import_type} == 0 ) then
  set file_ending = "mrc"
else
  set file_ending = "tiff"
endif
#
echo "<<@progress: 10>>"
#
set movie_average = ${movie_import_dir}/${movie_import_DC_averages}
set movie_stack   = ${movie_import_dir}/${movie_import_DC_stacks}
#
if ( ! -d ${movie_average} ) then
  ${proc_2dx}/protest "ERRROR: directory ${movie_average} not found."
endif
#
if ( ${movie_import_aligned_stack} == "y" ) then
  if ( ! -d ${movie_stack} ) then
    ${proc_2dx}/protest "ERRROR: directory ${movie_stack} not found."
  endif
endif
#
cd ${movie_average}
ls -1 *.${file_ending} > ${olddir}/tmp.tmp
cd ${olddir}
set imagenum = `cat tmp.tmp | wc -l`
echo ":: Importing ${imagenum} images"
#
set movie_import_dirname = `echo ${movie_import_dir} | rev | cut -d\/ -f1 | rev`
set movie_import_target_dirname = `echo ${movie_import_target_dir}-${movie_import_dirname} | sed -e 's/\./_/g' -e 's/\,/_/g' -e 's/ /_/g' -e's/:/_/g' -e's/#/_/g' -e 's/+/_/g'`
cd ..
if ( ! -d ${movie_import_target_dirname} ) then
  \mkdir ${movie_import_target_dirname}
  ${proc_2dx}/linblock "Creating ${movie_import_target_dirname}"
  cd ${movie_import_target_dirname}
  \ln -s ../2dx_master.cfg .
endif
cd ${olddir}
#
set irun = 1
while ( ${irun} <= ${imagenum} )
  set prog_num = `echo ${irun} ${imagenum} | awk '{ s = 10 + int( 80 * $1 / $2 ) } END { print s }'` 
  echo "<<@progress: ${prog_num}>>"
  #
  set importimage = `sed -n "${irun}p" tmp.tmp`
  if ( ${movie_import_type} == 0 ) then
    set importimage_pattern = `echo ${importimage} | sed -e's/_aligned.mrc//g' -e 's/\./-/g' -e 's/\,/-/g' -e 's/ /-/g' -e's/:/-/g' -e's/#/-/g' | tr 'A-Z' 'a-z'`
  else
    set importimage_pattern = `echo ${importimage} | sed -e's/_aligned.tiff//g' -e 's/\./-/g' -e 's/\,/-/g' -e 's/ /-/g' -e's/:/-/g' -e's/#/-/g' | tr 'A-Z' 'a-z'`
  endif
  #
  if ( -e ${movie_average}/${importimage} ) then
    if ( -e ${movie_stack}/${importimage} ) then
      set newval = `echo ${movie_import_imagenumber} | awk '{ s = $1 * 100 } END { print s }'`
      set testval = `echo ${newval} | wc -c`
      set newimagenumber = `echo 0000000000 | cut -c${testval}-`${newval}
      echo ":"
      echo ": Importing drift-corrected average image "
      echo ":      ${movie_average}/${importimage}"
      if ( ${movie_import_aligned_stack} == "y" ) then
        echo ": and drift-corrected stack"
        echo ":      ${movie_stack}/${importimage}"
      endif
      echo ":: ${irun}: Creating ${movie_import_target_dirname}/${importimage_pattern} for image number ${newimagenumber}"
      echo ":"
      cd ${olddir}
      cd ..
      cd ${movie_import_target_dirname}
      if ( -d ${importimage_pattern} ) then
        ${proc_2dx}/linblock "WARNING: ${importimage_pattern} already existing. Removing."
        \rm -rf ${importimage_pattern}
      endif
      mkdir ${importimage_pattern}
      cd ${importimage_pattern}
      cp ${movie_average}/${importimage} ${importimage_pattern}_${newimagenumber}_raw.${file_ending}
      if ( ${movie_import_aligned_stack} == "y" ) then
        cp ${movie_stack}/${importimage} ${importimage_pattern}_${newimagenumber}_stack.${file_ending}
      endif
      cp ${olddir}/2dx_merge.cfg 2dx_image.cfg
      echo "set imagename = ${importimage_pattern}_${newimagenumber}" >> 2dx_image.cfg
      echo "set nonmaskimagename = ${importimage_pattern}_${newimagenumber}" >> 2dx_image.cfg
      if ( ${movie_import_aligned_stack} == "y" ) then
        echo "set movie_stackname = ${importimage_pattern}_${newimagenumber}_stack.${file_ending}" >> 2dx_image.cfg
      endif
      echo "set imagenumber = ${newimagenumber}" >> 2dx_image.cfg
      echo "set imagename_original = ${movie_average}/${importimage}" >> 2dx_image.cfg
      echo "set initialization_reset = y" >> 2dx_image.cfg
      echo "set initialization_executable = y" >> 2dx_image.cfg
      set fullpath = `pwd`
      cd ${olddir}
      ${app_2dx_image} ${fullpath} "2dx_initialize"
      cd ${olddir}
      #
      @ movie_import_imagenumber++
      echo "set movie_import_imagenumber = ${movie_import_imagenumber}"  >> LOGS/${scriptname}.results
    else
      ${proc_2dx}/protest "ERROR: ${movie_stack}/${importimage} not found."
    endif
  else
    ${proc_2dx}/protest "ERROR: ${movie_average}/${importimage} not found."
  endif
  @ irun++
end
#
\rm -f tmp.tmp
#
echo "<<@progress: 100>>"
${proc_2dx}/linblock "Normal End."
#
echo "<<@evaluate>>"
#
