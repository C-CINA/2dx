#!/bin/csh -ef
####
#
#############################################################################
#                                                                           #
# Title: Process hkz                                                        #
#                                                                           #
# (C) 2dx.org, GNU Plublic License.                                         #
#                                                                           #
# Created..........: 21/01/2015                                             #
# Last Modification: 21/01/2015                                             #
# Author...........: Nikhil Biyani                                          #
#                                                                           #
#############################################################################
#
# SORTORDER: 22
#
# MANUAL: This script takes the unevenly spaced hkz file and creates a hkl file
#
# DISPLAY: SYM
# DISPLAY: realcell
# DISPLAY: realang
# DISPLAY: ALAT
# DISPLAY: sample_pixel
# DISPLAY: RESMIN
# DISPLAY: RESMAX
#
#$end_local_vars
#
set bin_2dx = ""
set proc_2dx = ""
#
set SYM = ""
set realcell = ""
set realang = ""
set ALAT = ""
set sample_pixel = ""
set RESMIN = ""
set RESMAX = ""
#
#$end_vars
set scriptname = 2dx_process_hkz
set split = ($realcell:as/,/ /)
set nx = $split[1]
set ny = $split[2]
#
echo "nx = ${nx}"
echo "ny = ${ny}"
echo "nz = ${ALAT}"
#
set date = `date`
echo date = ${date}
#
set ccp4_setup = 'y'
source ${proc_2dx}/initialize
#
echo "<<@progress: 10>"
#
#############################################################################
# Calling the script
#############################################################################
#
if ( -e APH/latlines.dat ) then
	set hkzFile = "APH/latlines.dat"
else
	set hkzFile = "SCRATCH/latlines.dat"
endif
#
${bin_2dx}/${scriptname}.exe ${hkzFile} ${SYM} ${nx} ${ny} ${ALAT} ${sample_pixel} ${RESMAX}
#
mv output.hkl APH/processed.hkl
#
echo "<<@progress: +70>>"
#
#
echo "# IMAGE: APH/processed.hkl <HKL: Generated HKL >" >> LOGS/${scriptname}.results
#
#############################################################################
${proc_2dx}/linblock "f2mtz - Program to convert hkl data into MTZ format"
#############################################################################
#
set infile = APH/processed.hkl
\rm -f SCRATCH/processed_f2mtz.mtz
#
${bin_ccp4}/f2mtz hklin ${infile} hklout SCRATCH/processed_f2mtz.mtz << eof
TITLE  P1 map, ${date}
CELL ${realcell} ${ALAT} 90.0 90.0 ${realang}
SYMMETRY 1
LABOUT H K L F PHI FOM
CTYPOUT H H H F P W
FILE ${infile}
SKIP 0
END
eof
#
echo "<<@progress: +5>>"
#
#
#############################################################################
${proc_2dx}/linblock "cad - to create MTZ file for volume"
#############################################################################  
#
\rm -f processed3D.mtz
#
${bin_ccp4}/cad hklin1 SCRATCH/processed_f2mtz.mtz hklout processed3D.mtz << eof
sort h k l
resolution overall ${RESMAX} ${RESMIN}
outlim spacegroup 1
labin file 1 all
valm NaN NOOUTPUT
end
eof
#
echo "<<@progress: +5>>"
#
echo "# IMAGE: processed3D.mtz <MTZ: Generated MTZ >" >> LOGS/${scriptname}.results
#
#############################################################################
${proc_2dx}/linblock "fft - to calculate map with X,Y,Z orientation"
#############################################################################
#
\rm -f processed3D.map
${bin_ccp4}/fft hklin processed3D.mtz mapout processed3D.map  << eot
LABIN F1=F  PHI=PHI W=FOM ##
AXIS X,Y,Z
SCALE F1 1 0
SYMMETRY 1
RESOLUTION ${RESMIN} ${RESMAX}
TITLE Sym=${SYM}, res=${RESMAX}, T=0
GRID 400 400 400
XYZLIM 0 399 0 399 0 399
RHOLIM 250.0
HKLMAX 199 199 199
END
eot
#
echo "# IMAGE: processed3D.map <MAP: Generated MAP >" >> LOGS/${scriptname}.results
#
echo "<<@progress: 100>>"
#
#
