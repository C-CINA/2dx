#!/bin/csh -ef
#############################################################################
#                                                                           #
# Title: Prepare movie mode script                                          #
#                                                                           #
# (C) 2dx.org, GNU Plublic License.                                         #
#                                                                           #
# Created..........: 10/01/2007                                             #
# Last Modification: 10/01/2007                                             #
# Author...........: 2dx.org                                                #
#                                                                           #
#############################################################################
#
# SORTORDER: 08
#
# MANUAL: Adapt this script to do some task on all selected image directories.
#
# MANUAL: To do so, double-click on the script name in the "Customs Script" panel. Edit the script, save it, and use it for your purposes.
#
#
# DISPLAY: movie_stack_folder
#
#$end_local_vars
#
# This defines some variables that we might need:
set proc_2dx = ""
set bin_2dx = ""
set app_2dx_image = ""
set SCRATCH_DISK = ""
set FFTIR_DISK = ""
set movie_stack_folder = ""
#
#$end_vars
#
# This sets the scriptname variable:
set scriptname = 2dx_moviemode_prepare
#
# This removes the old logfile, so that a new fresh one can be created:
\rm -f LOGS/${scriptname}.results
#
# This initializes the ccp4 environment, and other variables.
set ccp4_setup = 'y'
source ${proc_2dx}/initialize
#
# This advances the progress bar to the 1% position:
echo "<<@progress: 1>>"
#
# This creates all needed subdirectories in the current merge directory, in case they are missing:
source ${proc_2dx}/2dx_makedirs 
#
# This translates the list of directories to work on into one single long line:
cat 2dx_merge_dirfile.dat | tr "\n" " " > SCRATCH/2dx_merge_dirfile_oneline.dat
set dirlist = "`cat SCRATCH/2dx_merge_dirfile_oneline.dat`"
#
set tmp = `wc -l 2dx_merge_dirfile.dat`
set images_num = `echo ${tmp} | cut -d\  -f1`
set images_increment = `echo ${images_num} | awk '{ s = 80.0 / $1 } END { print s }'`
set images_current = 10.0
#
# This memorizes the current merge directory under the variable "olddir":
set olddir = $PWD
#
echo "<<@progress: 10>>"
#
# This initiates a big "foreach" loop that sets the variable "dirfile" to each of the items in the "dirlist" and runs the contents of the loop with this value:
foreach dirfile ( ${dirlist} ) 
  cd ..
  cd ${dirfile}
  set newdir = $PWD
  cd ${olddir}
  echo "::Working on ${newdir}"
  # This tells the 2dx_merge GUI that the following variable output concerns the image "newdir":
  echo "<IMAGEDIR="\"${newdir}\"">" >> LOGS/${scriptname}.results
  echo "set dummy = 1" >> LOGS/${scriptname}.results
  set images_progres = `echo ${images_current} | awk '{ s = $1 } END { printf "%.0f", s }'` 
  echo "<<@progres: ${images_progres}>>"
  set images_current = `echo ${images_current} ${images_increment} | awk '{ s = $1 + $2 } END { print s }'`
  
  set name = `python ${bin_2dx}/movie_mode_py/getOriginalImageName.py ${newdir}`
  echo name = ${name}
  
  if ( -e ${movie_stack_folder}/${name}) then
	${proc_2dx}/linblock "Movie mode for ${name} possible"
	time \cp -f ${movie_stack_folder}/${name} ${newdir}
	echo "set movie_enable = y" >> LOGS/${scriptname}.results
	echo "set movie_stackname = ${name}" >> LOGS/${scriptname}.results
  endif

  echo "</IMAGEDIR>" >> LOGS/${scriptname}.results
end
#
foreach dirfile ( ${dirlist} ) 
  cd ..
  cd ${dirfile}
  set newdir = $PWD
  cd ${olddir}
  echo "::Working on ${newdir}"
  # This tells the 2dx_merge GUI that the following variable output concerns the image "newdir":
  echo "<IMAGEDIR="\"${newdir}\"">" >> LOGS/${scriptname}.results
  echo "set dummy = 1" >> LOGS/${scriptname}.results
  set images_progres = `echo ${images_current} | awk '{ s = $1 } END { printf "%.0f", s }'` 
  echo "<<@progres: ${images_progres}>>"
  set images_current = `echo ${images_current} ${images_increment} | awk '{ s = $1 + $2 } END { print s }'`

  ${app_2dx_image} ${newdir} "2dx_initialize_files"
  
  echo "</IMAGEDIR>" >> LOGS/${scriptname}.results
end
#
# Move the progress bar to 100%:
echo "<<@progress: 100>>"
#
# Leave a message on the logfile that the script finished:
${proc_2dx}/linblock "${scriptname} finished correctly."
#
