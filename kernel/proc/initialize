#
# This is not an executable script.
#
# This should be called at the beginning of each 2dx script, to initialize the script environment.
#
echo ":++++++++++++++++++++++++++++++++"
echo ":Initializing script environment"
echo ":++++++++++++++++++++++++++++++++"
#
set date = `date`
echo date = ${date}
#
set ccp4 = /usr/local/ccp4
set bin_ccp4 = ${ccp4}/bin
#
echo bin_2dx = ${bin_2dx}
echo proc_2dx = ${proc_2dx}
echo ccp4 = ${ccp4}
echo bin_ccp4 = ${bin_ccp4}
if ($?app_2dx_image) then
  echo app_2dx_image = ${app_2dx_image}
endif
#
if ( $?ccp4_setup ) then
  if ( ${ccp4_setup} == 'y' ) then
    if ( ( ! -d ${ccp4} ) || ( ! -d ${bin_ccp4} ) ) then
      echo ":: "
      ${proc_2dx}/linblock "CCP4 not found. Is it installed ???"
      ${proc_2dx}/linblock "The location of CCP4 is defined in ${proc_2dx}/initialize"
      ${proc_2dx}/linblock "Currently, CCP4 it is expected in ${ccp4}"
      echo ":: "
      ${proc_2dx}/linblock "You will not be able to create a final map without CCP4."
      echo ":: "
      ${proc_2dx}/linblock "Install CCP4 from http://www.ccp4.ac.uk/download"
      echo ":: "
    else
      set ccp4_setup = ${bin_ccp4}/ccp4.setup-csh
      if ( ! -e ${ccp4_setup} ) then
        set ccp4_setup = ${ccp4}/include/ccp4.setup-csh
        if ( ! -e ${ccp4_setup} ) then
          set ccp4_setup = ${ccp4}/include/ccp4.setup
          if ( ! -e ${ccp4_setup} ) then
            set ccp4_setup = ${bin_ccp4}/ccp4.setup-csh
          endif
        endif
      endif
      if ( -e ${ccp4_setup} ) then
        # This is sourcing a script within a sourced script. Hopefully, this will work...
        echo ":proc/initialize: Sourcing ${ccp4_setup}"
        source ${ccp4_setup}
      else
        ${proc_2dx}/linblock "WARNING: ${ccp4_setup} does not exists."
        echo "#WARNING: ${ccp4_setup} does not exists." >> LOGS/${scriptname}.results
        ${proc_2dx}/linblock "CCP4 setup file not found. Is CCP4 correctly installed ???"
        ${proc_2dx}/linblock "It is expected in ${ccp4_setup}"
        ${proc_2dx}/linblock "The setup of CCP4 is defined in ${proc_2dx}/initialize"
        echo ":: "
        ${proc_2dx}/linblock "You will not be able to create a final map without CCP4."
        echo ":: "
        ${proc_2dx}/linblock "Please make sure that that file can be found."
        echo ":: "
      endif
    endif
  endif
endif
#

