#!/bin/csh -fe
####
#
#############################################################################
#                                                                           #
# Title: Unpack Stack                                                       #
#                                                                           #
# (C) 2dx.org, GNU Plublic License.                                         #
#                                                                           #
# Created..........: 03/21/2011                                             #
# Last Modification: 03/21/2011                                             #
# Author...........: 2dx.org                                                #
#                                                                           #
#############################################################################
#
# SORTORDER: 10
#
# MANUAL: <B>Welcome to the File Importer</B>
#
# DISPLAY: imagename_original
# DISPLAY: imagename
# DISPLAY: nonmaskimagename
# DISPLAY: imagenumber
# DISPLAY: tempkeep
# DISPLAY: crop
# DISPLAY: comment
# DISPLAY: crop_histogram
# DISPLAY: crop_histogram_percent
# DISPLAY: crop_histogram_stdev
# DISPLAY: movie_stackname
# DISPLAY: movie_enable
# DISPLAY: movie_imagenumber_total
# DISPLAY: movie_imagenumber_touse
# DISPLAY: movie_imagenumber_toave
# DISPLAY: import_rawstack
# DISPLAY: import_rawstack_type
# DISPLAY: import_gainref
# DISPLAY: import_gaincorrectedstack
# DISPLAY: import_produce_gainref2D
# DISPLAY: import_produce_gainref2Dfft
#
#$end_local_vars
#
# Static directory variables at disposition are:
# appDir_2dx
# scripts-standard_2dx
# scripts-custom_2dx
#
set bin_2dx = ""
set proc_2dx = ""
set SCRATCH_DISK = ""
#
# Variables to be available from the config file are:
set tempkeep = ""
set imagename = ""
set nonmaskimagename = ""
set crop = ""
set crop_histogram = ""
set crop_histogram_percent = ""
set crop_histogram_stdev = ""
set movie_stackname = ""
set import_rawstack = ""
set import_rawstack_type = ""
set import_gainref = ""
set import_gaincorrectedstack = ""
set import_produce_gainref2D = ""
set import_produce_gainref2Dfft = ""
set import_gaincorrectedstack_2d = ""
set import_gaincorrectedstack_2d_fft = ""
#
#$end_vars
#
# Input example: (these have extensions)
# set import_rawstack = "Prot_Sep29_16.59.59.mrc"
# set import_gainref = "CountRef_Prot_Sep28_15.27.14.dm4"
#
# Output example: (no extensions)
# set import_gaincorrectedstack = "Prot_Sep29_16_59_59_stack_raw"
# set import_gaincorrectedstack_2d = "Prot_Sep29_16_59_59_raw"
# set import_gaincorrectedstack_2d_fft = "Prot_Sep29_16_59_59_raw_fft"
#
# set imagename = "Prot_Sep29_16_59_59"
# set nonmaskimagename = "Prot_Sep29_16_59_59"
# set movie_stackname = "Prot_Sep29_16_59_59_stack_raw"
#
set scriptname = import_unpack
\rm -f LOGS/${scriptname}.results
#
set ccp4_setup = 'y'
source ${proc_2dx}/initialize
#
source ${proc_2dx}/2dx_makedirs
#
# Check the type of files provided. 
# Categories are:
# - raw movie, counted, non-drift-corrected, only dark-subtracted.
# - gain reference file, to multiply the dark-subtracted frames by.
# - drift-corrected movie
# - drift-corrected and averaged image
#
set namestub = `echo ${import_rawstack} | sed -e 's/\.mrc//g' -e 's/\.tif//g' | sed -e 's/\./_/g' -e 's/\,/_/g' -e 's/ /_/g' -e's/:/_/g' -e's/#/_/g' | tr 'A-Z' 'a-z'`
set import_gaincorrectedstack = ${namestub}_stack_raw
set import_gaincorrectedstack_2d = ${namestub}_raw
set import_gaincorrectedstack_2d_fft = ${namestub}_raw_fft
#
if ( ${import_rawstack_type} == "1" ) then
  if ( ! -e ${import_rawstack} ) then
    ${bin_2dx}/protest "ERROR: Raw stack ${import_rawstack} not found."
  endif    
  if ( ! -e ${import_gainref} ) then
    ${bin_2dx}/protest "ERROR: Gain reference ${import_gainref} not found."
  endif
  echo "::Raw movie as dark-subtracted stack present. Gain reference found."
  #
  echo "::Running: clip unpack ${import_rawstack} ${import_gainref} ${import_gaincorrectedstack}.mrc"
  clip unpack ${import_rawstack} ${import_gainref} ${import_gaincorrectedstack}.mrc
  #
  set movie_stackname = ${import_gaincorrectedstack}
  echo "set movie_stackname = ${movie_stackname}.mrc"  >> LOGS/${scriptname}.results
  echo "# IMAGE-IMPORTANT: ${movie_stackname}.mrc <Gain-corrected stack (stack)>" >> LOGS/${scriptname}.results
endif
#
if ( ${import_rawstack_type} == "2" ) then
  if ( ! -e ${import_gaincorrectedstack} ) then
    ${bin_2dx}/protest "ERROR: Gain-corrected stack ${import_gaincorrectedstack} not found."
  endif
  echo "::Raw movie as gain-corrected stack present."
  echo "::Running: clip unpack ${import_rawstack} ${movie_stackname}.mrc"
  clip unpack ${import_rawstack} ${movie_stackname}.mrc
  #
  set movie_stackname = ${import_gaincorrectedstack}
  echo "set movie_stackname = ${movie_stackname}"  >> LOGS/${scriptname}.results
  echo "# IMAGE-IMPORTANT: ${movie_stackname}.mrc <Gain-corrected stack (stack)>" >> LOGS/${scriptname}.results
endif
#
if ( ${import_rawstack_type} == "1" || ${import_rawstack_type} == "2" ) then
  echo "::Gain-corrected stack is ${movie_stackname}.mrc"
  set nonmaskedimagename = ${namestub}
  echo "set imagename = ${nonmaskedimagename}"  >> LOGS/${scriptname}.results
  echo "set nonmaskedimagename = ${nonmaskedimagename}"  >> LOGS/${scriptname}.results
endif
#
echo "<<@progress: 30>>"
#
if ( ${import_produce_gainref2D} == "y" ) then
  echo "::Running: avgstack on ${import_gaincorrectedstack}.mrc"
  avgstack << eot
${import_gaincorrectedstack}.mrc
${import_gaincorrectedstack_2d}.mrc
/
eot
  set nonmaskimagename = ${import_gaincorrectedstack_2d}
  echo "# IMAGE-IMPORTANT: ${nonmaskimagename}.mrc <Gain-corrected average image (2D)>" >> LOGS/${scriptname}.results
endif
#
#
if ( ${import_produce_gainref2D} == "y" && ${import_produce_gainref2Dfft} == "y" ) then
  echo "::Running: clip fft ${import_gaincorrectedstack_2d}.mrc ${import_gaincorrectedstack_2d_fft}.mrc"
  clip fft ${import_gaincorrectedstack_2d}.mrc ${import_gaincorrectedstack_2d_fft}.mrc
  echo "# IMAGE-IMPORTANT: ${import_gaincorrectedstack_2d_fft}.mrc <Gain-corrected average image FFT (2D)>" >> LOGS/${scriptname}.results
endif
#
##########################################################################
${proc_2dx}/linblock "${scriptname} - normal end."
##########################################################################
#
echo "<<@progress: 100>>"
echo "<<@evaluate>>"
#
exit
#
