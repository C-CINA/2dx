#!/bin/csh -ef
#############################################################################
#                                                                           #
# Title: Export Tomo Tilt Series                                            #
#                                                                           #
# (C) 2dx.org, GNU Plublic License.                                         #
#                                                                           #
# Created..........: 01/14/2017                                             #
# Last Modification: 01/14/2017                                             #
# Author...........: focus-em.org                                           #
#                                                                           #
#############################################################################
#
# SORTORDER: 50
#
# DISPLAY: export_tomo_basedir
#
#$end_local_vars
#
set bin_2dx = ""
set proc_2dx = ""
#
set comment = ""
set export_tomo_basedir = ""
#
#$end_vars
#
set scriptname = export_tiltseries
#
\rm -f LOGS/${scriptname}.results
source ${proc_2dx}/initialize
#
# This memorizes the current merge directory under the variable "olddir":
set olddir = $PWD
#
echo "<<@progress: 10>>"
#
cd ..
if ( ! -d ${export_tomo_basedir} ) then
  mkdir ${export_tomo_basedir}
endif
cd ${olddir}
#
# Count number of samples to export
set samplenum = `cat 2dx_merge_dirfile.dat  | cut -d\/ -f1 | sort | awk '\\!seen[$0]++' | wc -l`
set dirnum = `cat 2dx_merge_dirfile.dat | wc -l`
echo ": Will work on ${samplenum} specimens, in total ${dirnum} image directories."
#
set samplerun = 1
#
while ( ${samplerun} <= ${samplenum} )
  set cursample = `cat 2dx_merge_dirfile.dat  | cut -d\/ -f1 | sort | awk '\\!seen[$0]++' | head -n ${samplerun} | tail -n 1`
  # Count the number of tilt angles in for this sample
  cat 2dx_merge_dirfile.dat | sort | grep '^'${cursample} > SCRATCH/export_tiltseries_tmp1.txt
  set anglenum = `cat SCRATCH/export_tiltseries_tmp1.txt | wc -l`
  echo "Working on sample ${cursample}, which has ${anglenum} tilt angles."
  #
  # This translates the list of directories to work on into one single long line:
  cat SCRATCH/export_tiltseries_tmp1.txt | tr "\n" " " > SCRATCH/export_tiltseries_tmp2.txt
  set dirlist = "`cat SCRATCH/export_tiltseries_tmp2.txt`"
  \rm -f SCRATCH/export_tiltseries_tmp3.txt
  set stackname_base = ../${export_tomo_basedir}/${cursample}_stack
  set stackname = ${stackname_base}.mrcs
  \rm -f ${stackname}
  foreach dirfile ( ${dirlist} ) 
    #
    cd ${olddir}/../${dirfile}
    #
    ${proc_2dx}/lin "Working on ${dirfile}"
    #
    set export_tiltang = `grep "set TLTANG =" 2dx_image.cfg | head -1 | cut -d'"' -f2`
    set export_TLTANG = `echo ${export_tiltang} | awk '{ s = $1 + 5000 } END { print s }'`
    cd ${olddir}
    echo ${export_TLTANG} ${dirfile} >> SCRATCH/export_tiltseries_tmp3.txt   
  end
  sort SCRATCH/export_tiltseries_tmp3.txt > SCRATCH/export_tiltseries_tmp4.txt
  set framenum = `cat SCRATCH/export_tiltseries_tmp4.txt | wc -l`
  set curframe = 1
  while ( ${curframe} <= ${framenum} ) 
    set dirfile = `cat SCRATCH/export_tiltseries_tmp4.txt | cut -d\  -f2 | head -n ${curframe} | tail -n 1`
    if ( -e ${olddir}/../${dirfile}/movie_aligned.mrc ) then
      if ( ${curframe} == 1 ) then
        echo ":: Creating " ${stackname}
        cp ${olddir}/../${dirfile}/movie_aligned.mrc ${stackname}
      else
        ${dir_imod}/bin/addtostack << eot
${stackname}
0
1
${olddir}/../${dirfile}/movie_aligned.mrc
eot
      endif
    else
      ${proc_2dx}/linblock "WARNING: ${olddir}/../${dirfile}/movie_aligned.mrc not found."
    endif
    @ curframe += 1
  end
  #
  echo "::Tiltseries created for ${stackname}"
  echo "# IMAGE-IMPORTANT: ${stackname} <Tiltseries ${samplerun} MRC Stack>" >> LOGS/${scriptname}.results
  #
  #
  ############################################################################################################
  # Here to calculate a 3D reconstruction for this tilt series, and extract the central 2D slice from it.
  ############################################################################################################
  #
  # Something like:
  # imod --input ${stackname_base}.mrcs --output ${stackname_base}_3D.mrcs
  #
  #
  #
  #
  #
  echo "# IMAGE-IMPORTANT: ${stackname_base}_3D.mrcs <3D reconstruction ${samplerun}>" >> LOGS/${scriptname}.results
  echo "# IMAGE-IMPORTANT: ${stackname_base}_2D.mrc <Central slice ${samplerun}>" >> LOGS/${scriptname}.results
  #
  ############################################################################################################
  #
  @ samplerun += 1
end  
#
echo "<<@progress: 100>>"
echo "<<@evaluate>>"
#
##########################################################################
${proc_2dx}/linblock "${scriptname} - normal end."
##########################################################################
#
#
#
